plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'corda.common-publishing'

// We need to set mainClassName before applying the shadow plugin.
mainClassName = 'net.corda.tools.shell.standalone.StandaloneShellKt'
apply plugin: 'com.github.johnrengelman.shadow'

description 'Corda Standalone Shell'

configurations {
    cordaShellDependencies {}
}

dependencies {
    implementation project(":shell")
    implementation files(project(":shell").sourceSets.main.output)
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}

tasks.withType(JavaCompile).configureEach {
    // Resolves a Gradle warning about not scanning for pre-processors.
    options.compilerArgs << '-proc:none'
}

jar {
    enabled = false
    archiveClassifier = 'ignore'
}

shadowJar {
    archiveBaseName = 'corda-standalone-shell'
    archiveClassifier = ''
    mergeServiceFiles()
    manifest {
        attributes "Corda-Release-Version": cordaOSReleaseVersion
        attributes('Corda-Platform-Version': cordaPlatformVersion)
        attributes "Corda-Vendor": cordaBuildEdition
        attributes('Add-Opens': 'java.base/java.lang java.base/java.time java.base/java.io java.base/java.util')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'corda-standalone-shell'
            artifact(shadowJar) {
            }
        }
    }
}
